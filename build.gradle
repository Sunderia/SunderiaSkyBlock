plugins {
    id 'java'
    id 'kr.entree.spigradle' version '2.3.4'
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'de.undercouch.download' version '5.0.2'
}

//apply from: rootProject.file('buildtools.gradle')
apply from: rootProject.file('specialSource.gradle')

group 'fr.sunderia'
version '1.0-SNAPSHOT'

tasks.prepareSpigotPlugins.dependsOn shadowJar
tasks.runSpigot.dependsOn prepareSpigotPlugins
String mcVersion = '1.18.1'

shadowJar {
    configurations = [project.configurations.runtimeClasspath, project.configurations.shadow]
}

afterEvaluate {
    shadowJar {
        doLast {
            def outputFiles = outputs.files.files
            for (inputFile in outputFiles) {
                File intermediateFile = new File(temporaryDir, inputFile.name.replace('.jar', '-obf.jar'))
                File outputFile = inputFile
                remapMojangToSpigot(inputFile, intermediateFile, outputFile, "1.18.1-R0.1-SNAPSHOT")
            }
        }
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    protocolLib()
    maven {
        name = "Jeff"
        url = 'https://hub.jeff-media.com/nexus/repository/jeff-media-public/'
    }
    maven {
        name = "mine-maven"
        url = 'https://maven.galaxyfight.fr/snapshots'
    }
}

dependencies {
    compileOnly("org.spigotmc:spigot-api:$mcVersion-R0.1-SNAPSHOT")
    compileOnly "org.spigotmc:spigot:$mcVersion-R0.1-SNAPSHOT:remapped-mojang"
    compileOnly protocolLib('4.8.0')
    shadow 'fr.sunderia:SunderiaUtils:1.5.4-SNAPSHOT'
}

spigot {
    name = 'SunderiaSkyblock'
    version = mcVersion
    depends('ProtocolLib')
    permissions {
        'sunderiaskyblock.visibleholo' {
            description = 'Allows to use the visibleholo command.'
            defaults = 'op'
        }
        'sunderiaskyblock.test' {
            description = 'Allows to use the test command.'
            defaults = 'op'
        }
    }
    debug {
        args '--nojline', '--max-players', '4'
        jvmArgs '-Xmx4G'
        serverPort 25565
        eula = true
    }
    authors 'noalegeek68', 'minemobs'
    apiVersion '1.18'
}
