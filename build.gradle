plugins {
    id 'java'
    id 'kr.entree.spigradle' version '2.2.4'
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id 'de.undercouch.download' version '5.0.2'
}

apply from: rootProject.file('buildtools.gradle')

group 'org.example'
version '1.0-SNAPSHOT'

tasks.prepareSpigotPlugins.dependsOn shadowJar
tasks.runSpigot.dependsOn prepareSpigotPlugins

repositories {
    mavenCentral()
    mavenLocal()
    protocolLib()
    maven {
        name = "SunderiaRepo"
        url = "https://maven.galaxyfight.fr/snapshots"
    }
    maven {
        name = "Jeff"
        url = 'https://hub.jeff-media.com/nexus/repository/jeff-media-public/'
    }
}

dependencies {
    compileOnly('org.spigotmc:spigot-api:1.18.1-R0.1-SNAPSHOT')
    compileOnly 'org.spigotmc:spigot:1.18.1-R0.1-SNAPSHOT:remapped-mojang'
    compileOnly protocolLib('4.7.0')
    implementation 'fr.sunderia:SunderiaUtils:1.2-SNAPSHOT'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

spigot {
    name = 'SunderiaSkyblock'
    version = '1.18.1'
    depends('ProtocolLib')
    permissions {
        'sunderiaskyblock.visibleholo' {
            description = 'Allows to use the visibleholo command.'
            defaults = 'op'
        }
        'sunderiaskyblock.test' {
            description = 'Allows to use the test command.'
            defaults = 'op'
        }
    }
    debug {
        args '--nojline', '--max-players', '4'
        jvmArgs '-Xmx4G'
        serverPort 25565
        eula = true
    }
    authors 'noalegeek68', 'minemobs'
    apiVersion '1.18'
}